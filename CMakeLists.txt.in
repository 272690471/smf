cmake_minimum_required(VERSION 3.5)
project(build-deps NONE)

cmake_host_system_information(
  RESULT build_concurrency_factor
  QUERY NUMBER_OF_LOGICAL_CORES)
set(make_command make -j ${build_concurrency_factor})

include(ExternalProject)

ExternalProject_Add(boost_ext
  # The 1.67.0 release has a bug in Boost Lockfree around a missing header.
  URL https://dl.bintray.com/boostorg/release/1.64.0/source/boost_1_64_0.tar.gz
  URL_MD5 319c6ffbbeccc366f14bb68767a6db79
  PATCH_COMMAND
    ./bootstrap.sh
    --prefix=@SMF_DEPS_INSTALL_DIR@
    --with-libraries=atomic,chrono,date_time,filesystem,program_options,system,test,thread
  CONFIGURE_COMMAND ""
  BUILD_COMMAND ""
  INSTALL_COMMAND
    ${CMAKE_COMMAND} -E chdir <SOURCE_DIR>
    ./b2
    -j ${build_concurrency_factor}
    --layout=system
    --build-dir=<BINARY_DIR>
    install
    variant=debug
    link=shared
    threading=multi
    hardcode-dll-paths=true
    dll-path=@SMF_DEPS_INSTALL_DIR@/lib)

ExternalProject_Add(fmt_ext
  GIT_REPOSITORY https://github.com/fmtlib/fmt.git
  GIT_TAG        9e554999ce02cf86fcdfe74fe740c4fe3f5a56d5
  INSTALL_DIR    @SMF_DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DFMT_INSTALL=ON
    -DFMT_DOC=OFF
    -DFMT_TEST=OFF
  BUILD_COMMAND ${make_command})

ExternalProject_Add(protobuf_ext
  URL https://github.com/protocolbuffers/protobuf/releases/download/v3.3.0/protobuf-cpp-3.3.0.tar.gz
  URL_MD5 73c28d3044e89782bdc8d9fdcfbb5792
  INSTALL_DIR @SMF_DEPS_INSTALL_DIR@
  CONFIGURE_COMMAND <SOURCE_DIR>/configure --prefix=<INSTALL_DIR> --srcdir=<SOURCE_DIR>
  BUILD_COMMAND ${make_command})

# FIXME: this setup seems to cause dpdk to rebuild each time
set(dpdk_quadruple ${CMAKE_SYSTEM_PROCESSOR}-native-linuxapp-gcc)

set (dpdk_args
  EXTRA_CFLAGS=-Wno-error
  O=<BINARY_DIR>
  DESTDIR=@SMF_DEPS_INSTALL_DIR@
  T=${dpdk_quadruple})

ExternalProject_Add(dpdk_ext
  GIT_REPOSITORY https://github.com/scylladb/dpdk.git
  GIT_TAG a1774652fbbb1fe7c0ff392d5e66de60a0154df6
  CONFIGURE_COMMAND
    COMMAND
      ${CMAKE_COMMAND} -E chdir <SOURCE_DIR>
      make ${dpdk_args} config
    COMMAND
      ${CMAKE_COMMAND}
      -DSeastar_DPDK_CONFIG_FILE_IN=<BINARY_DIR>/.config
      -DSeastar_DPDK_CONFIG_FILE_CHANGES=@CMAKE_CURRENT_SOURCE_DIR@/dpdk_config
      -DSeastar_DPDK_CONFIG_FILE_OUT=<BINARY_DIR>/${dpdk_quadruple}/.config
      -P @CMAKE_CURRENT_SOURCE_DIR@/cmake/dpdk_configure.cmake
  BUILD_COMMAND ""
  INSTALL_COMMAND
   ${CMAKE_COMMAND} -E chdir <SOURCE_DIR>
   ${make_command} ${dpdk_args} install)

ExternalProject_Add(c-ares_ext
  URL https://c-ares.haxx.se/download/c-ares-1.13.0.tar.gz
  URL_MD5 d2e010b43537794d8bedfb562ae6bba2
  INSTALL_DIR @SMF_DEPS_INSTALL_DIR@
  CONFIGURE_COMMAND <SOURCE_DIR>/configure --prefix=<INSTALL_DIR> --srcdir=<SOURCE_DIR>
  BUILD_COMMAND ""
  INSTALL_COMMAND ${make_command} install)

ExternalProject_Add(yaml-cpp_ext
  URL https://github.com/jbeder/yaml-cpp/archive/yaml-cpp-0.5.3.tar.gz
  URL_MD5 2bba14e6a7f12c7272f87d044e4a7211
  INSTALL_DIR    @SMF_DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DCMAKE_PREFIX_PATH=<INSTALL_DIR>
    -DYAML_CPP_BUILD_TESTS=OFF
    -DBUILD_SHARED_LIBS=ON
  BUILD_COMMAND ${make_command}
  DEPENDS boost_ext)

ExternalProject_Add(seastar_ext
  GIT_REPOSITORY    https://github.com/scylladb/seastar.git
  GIT_TAG           9ed5fca9207f81662dd205b5c778890165959f3f
  INSTALL_DIR    @SMF_DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DCMAKE_PREFIX_PATH=<INSTALL_DIR>
    -DSeastar_INSTALL=ON
    -DSeastar_DPDK=ON
    -DSeastar_APPS=OFF
    -DSeastar_DEMOS=OFF
    -DSeastar_DOCS=OFF
    -DSeastar_TESTING=OFF
  BUILD_COMMAND ${make_command}
  DEPENDS fmt_ext boost_ext protobuf_ext dpdk_ext yaml-cpp_ext c-ares_ext)

ExternalProject_Add(zstd_ext
  GIT_REPOSITORY  https://github.com/facebook/zstd.git
  GIT_TAG         470344d33e1d52a2ada75d278466da8d4ee2faf6
  SOURCE_SUBDIR   "build/cmake"
  INSTALL_DIR    @SMF_DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DZSTD_MULTITHREAD_SUPPORT=OFF
    -DZSTD_LEGACY_SUPPORT=OFF
    -DZSTD_BUILD_STATIC=ON
    -DZSTD_BUILD_SHARED=OFF
    -DZSTD_BUILD_PROGRAMS=OFF
  BUILD_COMMAND ${make_command})

ExternalProject_Add(HdrHistogram_ext
  GIT_REPOSITORY  https://github.com/HdrHistogram/HdrHistogram_c.git
  GIT_TAG         59cbedec68d56ae2b484d45cd7ad2f930f2c9d91
  INSTALL_DIR    @SMF_DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DHDR_HISTOGRAM_BUILD_PROGRAMS=OFF
    -DHDR_HISTOGRAM_BUILD_SHARED=OFF
  BUILD_COMMAND ${make_command})

ExternalProject_Add(xxhash_ext
  GIT_REPOSITORY  https://github.com/Cyan4973/xxHash.git
  GIT_TAG         c9970b8ec4155e789f1c3682da923869a496ba9d
  SOURCE_SUBDIR   "cmake_unofficial"
  INSTALL_DIR    @SMF_DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DBUILD_XXHSUM=OFF
    -DBUILD_SHARED_LIBS=OFF
    -DBUILD_ENABLE_INLINE_API=ON
  BUILD_COMMAND ${make_command})

ExternalProject_Add(gtest_ext
  GIT_REPOSITORY  https://github.com/google/googletest.git
  GIT_TAG         644319b9f06f6ca9bf69fe791be399061044bc3d
  INSTALL_DIR    @SMF_DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
  BUILD_COMMAND ${make_command})

ExternalProject_Add(gflags_ext
  GIT_REPOSITORY  https://github.com/gflags/gflags.git
  GIT_TAG         1005485222e8b0feff822c5723ddcaa5abadc01a
  INSTALL_DIR    @SMF_DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DBUILD_SHARED_LIBS=OFF
    -DBUILD_STATIC_LIBS=ON
    -DBUILD_gflags_LIB=ON
    -DBUILD_TESTING=OFF
    -DBUILD_PACKAGING=OFF
  BUILD_COMMAND ${make_command})

ExternalProject_Add(glog_ext
  GIT_REPOSITORY  https://github.com/google/glog.git
  GIT_TAG         5d46e1bcfc92bf06a9ca3b3f1c5bb1dc024d9ecd
  INSTALL_DIR    @SMF_DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DBUILD_SHARED_LIBS=OFF
    -DWITH_GFLAGS=ON
  DEPENDS gflags_ext
  BUILD_COMMAND ${make_command})

ExternalProject_Add(googlebenchmark_ext
  GIT_REPOSITORY  https://github.com/google/benchmark.git
  GIT_TAG         eec9a8e4976a397988c15e5a4b71a542375a2240
  INSTALL_DIR    @SMF_DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DBENCHMARK_ENABLE_GTEST_TESTS=OFF
    -DBENCHMARK_ENABLE_TESTING=OFF
  DEPENDS gflags_ext
  BUILD_COMMAND ${make_command})

ExternalProject_Add(flatbuffers_ext
  URL https://github.com/google/flatbuffers/archive/b99332e.tar.gz
  URL_MD5 e1d499e557f4e3299ca5379d84d40686
  INSTALL_DIR    @SMF_DEPS_INSTALL_DIR@
  CMAKE_ARGS
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=<INSTALL_DIR>
    -DFLATBUFFERS_INSTALL=ON
    -DFLATBUFFERS_BUILD_FLATC=ON
    -DFLATBUFFERS_BUILD_TESTS=OFF
    -DFLATBUFFERS_BUILD_FLATHASH=OFF
  BUILD_COMMAND ${make_command})
